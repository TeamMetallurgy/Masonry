plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

base {
    archivesName = "${mod_name}-neoforge-${minecraft_version}"
}

version = "${version}${buildnumber}"

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft_version
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }

        server {
            server()
            programArgument '--nogui'
        }

        clientData {
            clientData()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    //compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    //runtimeOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"
}